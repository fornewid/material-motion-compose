import dependencies.Libs
import dependencies.Versions

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        buildConfig false
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "${Libs.Compose.version}"
    }
    lintOptions {
        textReport true
        textOutput 'stdout'
        checkReleaseBuilds false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    packagingOptions {
        // Multiple dependencies bring these files in. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }
}

dependencies {
    implementation project(':core')

    api Libs.Compose.navigation
    implementation Libs.Compose.activity
    implementation Libs.Compose.animation
    implementation Libs.Compose.foundation
    implementation Libs.Compose.material

    // ======================
    // Test dependencies
    // ======================

    androidTestImplementation Libs.AndroidX.Test.core
    androidTestImplementation Libs.AndroidX.Test.rules
    androidTestImplementation Libs.AndroidX.Test.runner
    androidTestImplementation Libs.AndroidX.Test.truth
    androidTestImplementation Libs.Compose.ui_test_junit4
    debugImplementation Libs.Compose.ui_test_manifest
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.fornewid.material-motion-compose'
                artifactId = 'navigation'
                version = '0.7.4'
            }
        }
    }
}
