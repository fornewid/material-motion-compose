plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.maven.publish)
    alias(libs.plugins.jetbrains.dokka)
    alias(libs.plugins.metalava)
}

android {
    namespace 'soup.compose.material.motion'
    compileSdkVersion buildConfig.compileSdk
    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        freeCompilerArgs += '-Xexplicit-api=warning'
    }
    buildFeatures {
        buildConfig false
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "${libs.versions.composeCompiler.get()}"
    }
    lint {
        textReport true
        textOutput file('stdout')
        checkReleaseBuilds false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    packagingOptions {
        resources {
            excludes += ['/META-INF/AL2.0', '/META-INF/LGPL2.1']
        }
    }
}

metalava {
    sourcePaths.setFrom("src/main")
    filename.set("api/current.api")
    reportLintsAsErrors.set(true)
}

dependencies {
    implementation libs.kotlin.stdlib
    implementation libs.compose.animation
    implementation libs.compose.runtime
    implementation libs.compose.ui.ui

    // ======================
    // Test dependencies
    // ======================

    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.compose.foundation
    androidTestImplementation libs.compose.ui.testJunit4
    debugImplementation libs.compose.ui.testManifest
    debugImplementation libs.androidx.tracing
}
